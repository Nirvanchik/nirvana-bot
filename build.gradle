plugins {
    id 'java'
    id 'maven'
    id 'application'
}

group = 'org.wikipedia'
version = '1.0'

description = """"""

sourceCompatibility = 8
targetCompatibility = 8
tasks.withType(JavaCompile) {
    options.encoding = 'UTF-8'
}

repositories {
        
     maven { url "https://repo.maven.apache.org/maven2" }
}

dependencies {
    compile group: 'com.fasterxml.jackson.core', name: 'jackson-annotations', version: '2.9.10'
    compile group: 'com.fasterxml.jackson.core', name: 'jackson-core', version: '2.9.10'
    compile group: 'com.fasterxml.jackson.core', name: 'jackson-databind', version: '2.9.10.1'
    compile group: 'org.apache.commons', name: 'commons-lang3', version: '3.5'
    compile group: 'commons-collections', name: 'commons-collections', version:'3.2.1'
    compile group: 'org.apache.logging.log4j', name: 'log4j-api', version: '2.11.0'
    compile group: 'org.apache.logging.log4j', name: 'log4j-core', version: '2.11.0'
    compile group: 'com.google.code.findbugs', name: 'jsr305', version: '3.0.2'

    compileOnly group: 'javax.servlet', name: 'javax.servlet-api', version: '4.0.1'

    testCompile group: 'com.googlecode.json-simple', name: 'json-simple', version: '1.1.1'
    testCompile group: 'org.mockito', name: 'mockito-all', version: '1.10.19'
    testCompile group: 'junit', name: 'junit', version: '4.12'
    testCompile group: 'com.github.tomakehurst', name: 'wiremock-jre8-standalone', version: '2.25.1'

}

jar {
    manifest {
        attributes 'Main-Class': 'org.wikipedia.nirvana.nirvanabot.NirvanaBot'
    }
}

mainClassName = 'org.wikipedia.nirvana.nirvanabot.NirvanaBot'
executableDir = 'bin'

task createStatisticsBotRunApp(type: CreateStartScripts) {
    mainClassName = 'org.wikipedia.nirvana.statistics.StatisticsBot'
    classpath = startScripts.classpath
    outputDir = startScripts.outputDir
    applicationName = 'statistics-bot'
    defaultJvmOpts = startScripts.defaultJvmOpts
}

task createArchiveBotRunApp(type: CreateStartScripts) {
    mainClassName = 'org.wikipedia.nirvana.archive.NirvanaArchiveBot'
    classpath = startScripts.classpath
    outputDir = startScripts.outputDir
    applicationName = 'archive-bot'
    defaultJvmOpts = startScripts.defaultJvmOpts
}

task createCleanArchiveBotRunApp(type: CreateStartScripts) {
    mainClassName = 'org.wikipedia.nirvana.cleanarchive.CleanArchiveBot'
    classpath = startScripts.classpath
    outputDir = startScripts.outputDir
    applicationName = 'clean-archive-bot'
    defaultJvmOpts = startScripts.defaultJvmOpts
}

applicationDistribution.into('bin') {
    duplicatesStrategy = DuplicatesStrategy.EXCLUDE
    from(createStatisticsBotRunApp, createArchiveBotRunApp, createCleanArchiveBotRunApp)
    fileMode = 0755
}

distributions {
    main {
        baseName = 'NirvanaBot'
        contents {
            from('.') {
                include (
                    '*.cmd',
                    'README.MD', 'README_RU.MD', 'release-notes-ru.txt',
                    '*.xml',
                    'COPYING', 'NOTICE', 'Apache_License.txt',
                    'task.txt', 'task_debug.txt', 'task_debug_be.txt', 'task_debug_uk.txt')
                exclude 'loginNirvanaBot.xml', 'loginNirvanaTestBot.xml', 'pom.xml'
            }
            from('statistics') {
                include ('*')
                into('statistics')
            }
            from('config') {
                include ('*.properties')
                into ('config')
            }
            from('translations') {
                include ('*.ini')
                into ('translations')
            }
            from('templates') {
                include ('*')
                into ('templates')
            }
        }
    }
}

afterEvaluate { project ->
    // To correctly put files with Cyrillic file names in zip.
    distZip.metadataCharset = 'UTF-8'
}

tasks.create('dist7z') {
    group = 'distribution'
    description = 'Bundles the project as a distribution to 7z (the same as distZip).'
    Task distZipTask = tasks['distZip']
    dependsOn distZipTask

    doLast {
        logger.lifecycle "Unzip distribution: ${distZipTask.archivePath}"
        def sevenZip = '7z'
        def tmpDir = "${project.buildDir}/7z"
        mkdir tmpDir
        exec {
            executable '7z'
            args 'x', distZipTask.archivePath
            workingDir tmpDir
            standardOutput System.out
            errorOutput System.err
        }
        def archive = distZipTask.archivePath.path.replace('.zip', '.7z')
        logger.lifecycle "Compress to 7z: ${archive}"
        exec {
            executable '7z'
            args 'a', archive, distZipTask.archiveName.replace('.zip','')
            workingDir tmpDir
            standardOutput System.out
            errorOutput System.err
        }
        delete tmpDir
    }
}

